RULE STOP_REQUEST wait on UpRequested
CLASS org.jboss.msc.service.ServiceInstanceImpl$DependencyStoppedTask
METHOD run
AT ENTRY
BIND NOTHING
IF incrementCounter("run rule only once") == 1
DO
   # DependencyStoppedTask will be able of acquiring the ServiceInstanceImpl lock only after
   # upperCount is incremented to 1
   debug("wait for UpRequested"),
   signalWake("StopRequested", true),
   waitFor("UpRequested", 100000),
   debug("go!!!")
ENDRULE

RULE Signal UpRequested
CLASS org.jboss.msc.service.ServiceInstanceImpl 
METHOD setMode
AT EXIT
BIND NOTHING
IF  $0.upperCount > 0 && $0.state.toString().equals("STOP_REQUESTED")
DO
    # signal UpRequested, making the service controller to enter the exact transition that we need to
    # test: STOP_REQUESTED_to_UP
    debug("signalling UpRequested"),
    signalWake("UpRequested", true),
    debug("signalled!")
ENDRULE

RULE changeMode after STOP_REQUESTED
CLASS org.jboss.msc.service.ServiceInstanceImpl
METHOD setMode
AT ENTRY
BIND NOTHING
IF $1 == org.jboss.msc.service.ServiceController$Mode.ACTIVE AND NOT $0.state.toString().equals("NEW")
DO
   # Make sure that there is enough time for ServiceInstance to enter STOP_REQUESTED state, before
   # we set the mode to ACTIVE
   waitFor("StopRequested", 100000),
   debug("go")
ENDRULE